
// Функция проверяет переданную строку, представляющую собой ИНН, на правильность,
// используя проверку контрольного символа и длины переданной строки
//
// Параметры:
//  ИНН - Строка - проверяемый ИНН
// 
// Возвращаемое значение:
//  Истина - переданный ИНН верен
//				или его длина равна 12 символам
//	Ложь   - переданный ИНН не прошел проверку контрольного символа
//				или содержит в представлении не 10 символов
//
Функция ИННВерен(Знач ИНН) Экспорт
	
	Если ТипЗнч(ИНН) <> Тип("Строка") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ИНН = СокрЛП(ИНН);
	
	ДлинаИНН = СтрДлина(ИНН);
	
	Если ДлинаИНН = 12 Тогда
		
		Возврат Истина;
		
	ИначеЕсли ДлинаИНН = 10 Тогда
		
		КонтрольныйСимволИНН = РассчитатьКонтрольныйСимволИНН(ИНН);
		
		Возврат (Прав(ИНН, 1) = КонтрольныйСимволИНН);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция РассчитатьКонтрольныйСимволИНН(Знач ИНН)
	
	КоэффициентыРазрядов = Новый Массив;
	КоэффициентыРазрядов.Добавить(2);
	КоэффициентыРазрядов.Добавить(4);
	КоэффициентыРазрядов.Добавить(10);
	КоэффициентыРазрядов.Добавить(3);
	КоэффициентыРазрядов.Добавить(5);
	КоэффициентыРазрядов.Добавить(9);
	КоэффициентыРазрядов.Добавить(4);
	КоэффициентыРазрядов.Добавить(6);
	КоэффициентыРазрядов.Добавить(8);
	
	Делитель         = 11;
	КонтрольнаяСумма = 0;

	Для Счетчик = 1 По 9 Цикл
		
		КоэффициентРазряда = КоэффициентыРазрядов[Счетчик - 1];
		
		СимволИНН = Сред(ИНН, Счетчик, 1);
		
		ЦифраИНН = Число(СимволИНН);
		
		КонтрольнаяСумма = КонтрольнаяСумма + КоэффициентРазряда * ЦифраИНН;
		
	КонецЦикла;
	
	КонтрольныйРазряд = КонтрольнаяСумма % Делитель;
	
	Если КонтрольныйРазряд = 10 Тогда
		
		КонтрольныйРазряд = 0;
		
	КонецЕсли;
	
	Возврат Строка(КонтрольныйРазряд);
	
КонецФункции